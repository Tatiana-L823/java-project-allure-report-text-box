plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

repositories {
    mavenCentral()
}

def allureVersion = "2.21.0",
    selenideVersion = "6.19.1",
    ownerVersion = "1.0.4"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
}
compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:5.10.0",
            "org.aeonbits.owner:owner:$ownerVersion",
            "com.github.javafaker:javafaker:1.0.2",
            "org.slf4j:slf4j-simple:2.0.7"
    )
}

test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    finalizedBy 'allureReport' // генерировать отчёт после тестов

    testLogging {
        events "started", "skipped", "failed", "standard_error", "standard_out"
        exceptionFormat "short"
    }
}

task positive_test(type: Test) {
    useJUnitPlatform {
        includeTags("positive")
    }
    finalizedBy 'allureReport'
}

task negative_test(type: Test) {
    useJUnitPlatform {
        includeTags("negative")
    }
    finalizedBy 'allureReport'
}
